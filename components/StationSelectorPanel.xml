<?xml version="1.0" encoding="utf-8"?>
<component name="StationSelectorPanel" extends="Group" initialFocus="rowList">

	<script type="text/brightscript" uri="pkg:/components/StationSelectorPanel.brs" />
	<script type="text/brightscript" uri="pkg:/components/search.brs" />
	<script type="text/brightscript" uri="pkg:/components/StationActions.brs" />
	<script type="text/brightscript" uri="pkg:/source/utils/DeviceUtils.brs" />

	<interface>
	</interface>

	<script type="text/brightscript">
		<![CDATA[
		sub init()
			m.rowlist = m.top.findNode("rowList")
			m.content = createObject("roSGNode", "ContentNode")
			m.rowlist.content = m.content

            m.rowlist.ObserveField("rowItemSelected", "rowItemSelected")
            m.global.ObserveField("reloadStations", "reloadStations")

			setupStations()

			'm.top.setFocus(true)
		end sub

		sub setupDirectoryButtons()
			buttons = createObject("roSGNode", "ContentNode")
			buttons.title = "More Stations"

			browseButton = createObject("roSGNode", "ContentNode")
			browseButton.title = "Browse"

			searchButton = createObject("roSGNode", "ContentNode")
			searchButton.title = "Search"

			featuredStationsButton = createObject("roSGNode", "ContentNode")
			featuredStationsButton.title = "Featured"

			buttons.appendChild(browseButton)
			buttons.appendChild(searchButton)
			buttons.appendChild(featuredStationsButton)

			m.content.insertChild(buttons, 3)
		end sub

		sub rowItemSelected(item)
			' print "rowItemSelected"
			
				currentItemLocation = item.getData()
				currentRow = currentItemLocation[0]
				currentItem = currentItemLocation[1]

				if currentRow = 3
					if currentItem = 0
						navigateToSearch()
					else if currentItem = 1
						navigateToBrowse()
					else if currentItem = 2
						navigateToFeaturedStations()
					end if
					return
				end if

				rowData = m.rowlist.content.getChild(currentRow)
				if rowData = invalid
					return
				end if

				station = rowData.getChild(currentItem)

				if currentRow = 0
					PlayStation(station)
				else
					showStationPlayDialog(station)
				end if
		end sub

		sub rowItemFocused(item)
			'cell = item.getRoSGNode()
			'if cell <> invalid
			'	cell.itemlabel.text = index[0].ToStr()
			'end if
		end sub

		function showSetupDialog()
			m.webServerInstructionsLabel = m.top.findNode("webServerInstructionsLabel")
			ipAddress = DeviceIPAddress()
			message = "To add stations or setup The Bat Player visit http://" + ipAddress + ":9999 with your computer's web browser."

			dialog = createObject("roSGNode", "Dialog")
			dialog.title = "Setup The Bat Player"
			dialog.message = message
			m.global.scene.dialog = dialog
		end function

		function reloadStations(event)
			print "reloadStations(event)"
			setupStations()
		end function

		function onKeyEvent(key as String, press as Boolean) as Boolean
			print "StationSelector#onKeyEvent"
			
			if key = "back"
				if m.childScreen <> invalid
					m.top.removeChild(m.childScreen)
					m.childScreen = invalid
					m.rowlist.setFocus(true)
					return true
				end if
			end if

			if key = "options"
				showSetupDialog()
				return true
			end if

			return false
        end function

		]]>
	</script>

	<children>

		<Poster
			id="headerPoster"
			uri="pkg:/images/StationSelectionBackgroundTexture.png"
			width="1280" height="720"
			translation="[0,0]"
			opacity="0.75"
		/>

		<BatPlayerRowList
			id="rowList"
			showRowCounter = "[true, true, true, false]"
			translation="[100,130]"
		/>

		<BatOverhang
			id="overhang"
		/>

			
	</children>
	
</component>